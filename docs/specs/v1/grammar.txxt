Grammar for txxt

    This document describes the formal grammar for the txxt language, outlining its syntax and structural rules.

    The grammar is defined using a combination of Backus-Naur Form (BNF) and descriptive text to provide clarity on the language constructs.


1. Notation

    This section documents the notation systems used in txxt specifications.

    1.1. Grammar Notation

        txxt specs use a simplified BNF-like notation for describing syntax patterns:

        Occurrence Indicators:
        - `<element>?` - Optional (0 or 1 occurrence)
        - `<element>*` - Zero or more occurrences
        - `<element>+` - One or more occurrences
        - `<element>{n}` - Exactly n occurrences
        - `<element>{n,m}` - Between n and m occurrences
        - `<element>{n,*}` - n or more occurrences

        Sequence Operators:
        - `A B` - A followed by B
        - `A | B` - A or B (alternatives)
        - `(A B)` - Grouping

        Examples:
            <text-line> = <text-span>+ <line-break>
            <paragraph> = <text-line>+
            <list-marker> = <dash> | <number> <period> | <letter> <period>
            <session-title> = (<number> <period>)? <text-span>+ <line-break>
        :: grammar

    1.2. AST Tree Notation

        AST structures are shown using ASCII tree notation:

        Example AST structure:
            ├── session
            │   ├── session-title
            │   └── container
            │       ├── paragraph
            │       └── list
            │           ├── list-item
            │           └── list-item
        :: tree

        Conventions:
        - `├──` indicates a child node
        - `│` indicates continuation of parent structure
        - `└──` indicates the last child at a level
        - Indentation shows nesting depth

    1.3. Token Pattern Notation

        Individual token patterns use regular expression-like syntax:

        - `\s` - whitespace character
        - `\n` - line break
        - `[a-z]` - character class
        - `+` - one or more
        - `*` - zero or more
        - `?` - optional
        - `^` - start of line
        - `$` - end of line

        Examples:
            SequenceMarker: ^- \s
            AnnotationMarker: :
            VerbatimStart: .+:\s*$
        :: regex

    1.4. Indentation Notation

        Indentation levels are shown with explicit markers:

        Indentation example:
            Base level (column 0)
                +1 indented (column 4)
                    +2 indented (column 8)
        :: indentation

        - Each indentation level is exactly 4 spaces
        - `+n` indicates n levels of indentation from base
        - Tabs are converted to 4 spaces during preprocessing

2. Tokens

    Tokens are the atomic units of the txxt language, from single characters to complete lines.

    2.1. Character Tokens

        2.1.1. txxt-marker

            <txxt-marker> = ':' ':'

            The only explicit syntax element in txxt, defined by two consecutive colons (::). This marker is used to denote special constructs within the document, such as annotations.

        2.1.2. Whitespace

            <space> = ' '
            <tab> = '\t'
            <whitespace> = <space> | <tab>
            <line-break> = '\n'

        2.1.3. Sequence Markers

            <dash> = '-'
            <period> = '.'
            <open-paren> = '('
            <close-paren> = ')'
            <colon> = ':'

    2.2. Composite Tokens

        2.2.1. Indentation

            <indent-step> = <space>{4} | <tab>
            <indent-token> = <indent-step>
            <dedent-token> = (generated in later transformation phase)
            
            Note: The lexer generates simple indent tokens (one per 4 spaces or tab).
            Semantic indent/dedent tokens are generated in a later transformation step.

        2.2.2. Sequence Decorations

            <plain-marker> = <dash> <space>

            <number> = [0-9]+
            <letter> = [a-zA-Z]
            <roman-numeral> = 'I' | 'II' | 'III' | 'IV' | 'V' | ...

            <separator> = <period> | <close-paren>
            
            <ordered-marker> = (<number> | <letter> | <roman-numeral>) <separator> <space>
            <list-item-marker> = <plain-marker> | <ordered-marker>
            <session-title-marker> = <ordered-marker>
        2.2.3. Subject Markers
            <subject> = <colon>
        2.2.4. Text Spans
            <text-char> = (any character except <line-break>)
            <text-span> = <text-char>+

    2.3. Line Tokens
        2.3.1 Core Line Types
            <plain-dashed-line> = <indent>? <plain-marker> <text-span>+ <line-break>
            <ordered-line> = <indent>? <ordered-marker> <text-span>+ <line-break>
            <subject-line> = <indent>?  <text-span>+ <subject> <line-break>
            <blank-line> = <whitespace>* <line-break>
            <text-line> = <indent>? <text-span>+ <line-break>
        2.3.2 Composite Line Types:
            <list-item-line> = <plain-dashed-line> | <ordered-line>
            <session-title-line> = <ordered-line> | <subject-line> | <text-line>
            <any-line> = <list-item-line> | <session-title-line> | <text-line>


3. Element Grammar

    These are the core elements of txxt: annotations, sessions, paragraphs, lists, and definitions:

    <annotation> = <annotation-marker> <annotation-opening> <annotation-content>? <annotation-closing>
    <annotation-marker> = "::"
    <annotation-opening> = <label> <parameters>?
    <label> = <letter> (<letter> | <digit> | "_" | "-" | ".")*
    <parameters> = (<parameter> ","?)+
    <parameter> = <key> ("=" <value>)?
    <key> = <letter> (<letter> | <digit> | "_" | "-")*
    <value> = <quoted-string> | <unquoted-value>
    <annotation-content> = <indent> (<paragraph> | <list>)+ <dedent>
    <annotation-closing> = <annotation-marker>

    Note: Annotations have three forms:
    - Marker form: :: label :: (no content)
    - Single-line form: :: label :: inline text
    - Block form: :: label \n <indent>content<dedent> ::
    Content cannot include sessions or nested annotations.

    <list> = <blank-line> <list-item-line>{2,*}

    Note: Lists require a preceding blank line for disambiguation. This means:
    - A list must start after a blank line (or at document start)
    - Blank lines between list items are NOT allowed (would terminate the list)
    - Single list-item-lines become paragraphs (not lists)

    <definition> = <subject-line> <indent> <definition-content>
    <definition-content> = (<paragraph> | <list>)+

    Note: Definitions differ from sessions in two key ways:
    - NO blank line between subject and content (immediate indent)
    - Content cannot include sessions (only paragraphs and lists)
    - Subject line must end with a colon (:)

    <session> = <session-title-line> <blank-line> <indent> <session-content>
    <session-content> = (<paragraph> | <list> | <session>)+

    <foreign-block> = <subject-line> <foreign-content>? <closing-annotation>
    <subject-line> = <text-span>+ <colon> <line-break>
    <foreign-content> = <indent> <raw-text-line>+ <dedent>
    <raw-text-line> = <indent>? <any-character>+ <line-break>
    <closing-annotation> = <annotation-marker> <label> <parameters>? <annotation-marker>

    Note: Foreign blocks have two forms:
    - Block form: subject + indented content + closing annotation
    - Marker form: subject + closing annotation (no content)
    The "Indentation Wall" rule applies: content must be indented deeper than subject,
    and closing annotation must be at same level as subject.

    <paragraph> = <any-line>+

    <document> = <metadata>? <content>
    <metadata> = (document metadata, non-content information)
    <content> = (<foreign-block> | <annotation> | <paragraph> | <list> | <definition> | <session>)*

    Parse order: <foreign-block> | <annotation> | <list> | <definition> | <session> | <paragraph>
